/*******************************************************************************************
  SIMPL+ Module Information
  (Fill in comments below)
*******************************************************************************************/
/*
Dealer Name: ASEI
System Name: Aten US3384i
System Number:
Programmer: Pat Santucci
Comments:
*/

/*******************************************************************************************
  Compiler Directives
  (Uncomment and declare compiler directives as needed)
*******************************************************************************************/
// #ENABLE_DYNAMIC
// #SYMBOL_NAME ""
#SYMBOL_NAME "Aten US3384i USB 3.2 Switcher"
// #HINT ""
#HINT "Full control with feedback via RS-232 or RS-485"

// #DEFINE_CONSTANT
// #CATEGORY ""
#CATEGORY "23" // Switcher
 
// #PRINT_TO_TRACE
// #DIGITAL_EXPAND 
// #ANALOG_SERIAL_EXPAND 
// #OUTPUT_SHIFT 
// #HELP_PDF_FILE ""
#DEFAULT_VOLATILE
#ENABLE_STACK_CHECKING
// #ENABLE_TRACE
// #ENCODING_ASCII
// #ENCODING_UTF16
// #ENCODING_INHERIT_FROM_PARENT
// #ENCODING_INHERIT_FROM_PROGRAM

#HELP_BEGIN
Controls ATEN USB 3.2 Switcher US3384i
Tested against firmware V1.0.062

Inputs:

poll_connected: query each host port for USB connection
poll_status: catch-all query. Updates current selected port and its connection
status, auto-switching (Power On Detect) state, and priority fallback port
for auto-switching.

priority_none: works with analog input "priority". When auto-switching is
active, a fallback port can be set to always take when the active port's
host is disconnected. Pulse priority_none to revert to the default behavior and
take the next higher number connected port. Priority has no effect when
auto-switching is disabled.
auto_switch_enable: Pulse to enable automatic switch to a newly detected USB
connection. Called "power on detect" in the ATEN manual, but also works for
physical connection of a host. Disconnect behavior is controlled by priority
settings.
auto_switch_disable: Pulse to disable automatic switching.

host_take[x]: Pulse to switch to host port[x]


RX$: Connect to rx$ of a COM serial driver. RS-232 settings are fixed in
firmware.
Baud Rate: 115200
Data Bit: 8 bit
Parity: None
Stop Bit: 1 bit
Flow Control: none
Refer to the device user manual for RS-485 or custom firmware for different
serial settings.

host: on change to a value between 1-8 inclusive, switches to that host port
like the digital host_take[x] prompts. Included as a convenience for
programs that drive switchers with analog vaules.

priority: works with digtial input "priority_none". See that entry for a
description of priority port behavior. Setting a value 1-8 inclusive makes that
port the priorty and unsets priority_none.


Outputs:

priority_none_fb: high when priority is none, low if priority is set to a value
1-8.
auto_switch_enable_fb: high when auto-switching (power on detect) is
enabled.
auto_switch_disable_fb: high when auto-switching (power on detect) is
disabled.

host_taken_fb[x]: signal [x] is high to reflect the currenly active host port
(1-8). Others are low.

host_connected_fb[x]: signal [x] is high for every host port with an connected
and powered on host.

TX$: Connect to tx$ of a COM serial driver. See RX$ for settings.

host_fb: value 1-8 to reflect the currenly active host port. Included as a
convenience for programs that drive switchers with analog vaules.
priority_fb: value 1-8 to reflect the current priority port, value 0 if the
priority port is set to none. See digital input "priority_none" for a
description of priority port behavior.

#HELP_END

/*******************************************************************************************
  Include Libraries
  (Uncomment and include additional libraries as needed)
*******************************************************************************************/
// #CRESTRON_LIBRARY ""
// #USER_LIBRARY ""

/*******************************************************************************************
  DIGITAL, ANALOG and SERIAL INPUTS and OUTPUTS
  (Uncomment and declare inputs and outputs as needed)
*******************************************************************************************/
// DIGITAL_INPUT 
// ANALOG_INPUT 
// STRING_INPUT 
// BUFFER_INPUT 
BUFFER_INPUT RX$[255];
STRING_INPUT _SKIP_;
DIGITAL_INPUT poll_connected;
DIGITAL_INPUT poll_status;
DIGITAL_INPUT _SKIP_;
DIGITAL_INPUT priority_none;
DIGITAL_INPUT auto_switch_enable;
DIGITAL_INPUT auto_switch_disable;
DIGITAL_INPUT _SKIP_;
DIGITAL_INPUT host_take[8];
DIGITAL_INPUT _SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_,_SKIP_;

ANALOG_INPUT host;
ANALOG_INPUT priority;
// DIGITAL_OUTPUT 
// ANALOG_OUTPUT 
// STRING_OUTPUT
STRING_OUTPUT TX$;
STRING_OUTPUT _SKIP_;
DIGITAL_OUTPUT _SKIP_,_SKIP_,_SKIP_;
DIGITAL_OUTPUT priority_none_fb;
DIGITAL_OUTPUT auto_switch_enable_fb;
DIGITAL_OUTPUT auto_switch_disable_fb;
DIGITAL_OUTPUT _SKIP_;
DIGITAL_OUTPUT host_taken_fb[8];
DIGITAL_OUTPUT _SKIP_;
DIGITAL_OUTPUT host_connected_fb[8];
DIGITAL_OUTPUT _SKIP_;
ANALOG_OUTPUT host_fb;
ANALOG_OUTPUT priority_fb;


/*******************************************************************************************
  SOCKETS
  (Uncomment and define socket definitions as needed)
*******************************************************************************************/
// TCP_CLIENT
// TCP_SERVER
// UDP_SOCKET

/*******************************************************************************************
  Parameters
  (Uncomment and declare parameters as needed)
*******************************************************************************************/
// INTEGER_PARAMETER
// SIGNED_INTEGER_PARAMETER
// LONG_INTEGER_PARAMETER
// SIGNED_LONG_INTEGER_PARAMETER
// STRING_PARAMETER

/*******************************************************************************************
  Parameter Properties
  (Uncomment and declare parameter properties as needed)
*******************************************************************************************/
/*
#BEGIN_PARAMETER_PROPERTIES parameter_variable, parameter_variable, ...
   // propValidUnits = // unitString or unitDecimal|unitHex|unitPercent|unitCharacter|unitTime|unitTicks;
   // propDefaultUnit = // unitString, unitDecimal, unitHex, unitPercent, unitCharacter, unitTime or unitTicks;
   // propBounds = lower_bound , upper_bound;
   // propDefaultValue = ;  // or, propDefaultValue = "";
   // propList = // { "value" , "label" } , { "value" , "label" } , ... ;
   // propShortDescription = "status_bar_hint_text";
   // #BEGIN_PROP_FULL_DESCRIPTION  line_1...  line_2...  line_n  #END_PROP_FULL_DESCRIPTION
   // #BEGIN_PROP_NOTES line_1...  line_2...  line_n  #END_PROP_NOTES
#END_PARAMETER_PROPERTIES
*/

/*******************************************************************************************
  Structure Definitions
  (Uncomment and define structure definitions as needed)
  Note:  Be sure to initialize all declared STRING variables as needed
         For example, in Function Main: struct.myString = "";
*******************************************************************************************/
/*
STRUCTURE MyStruct1
{
};

MyStruct1 struct;
*/

/*******************************************************************************************
  Global Variables
  (Uncomment and declare global variables as needed)
  Note:  Be sure to initialize all declared STRING variables as needed
         For example, in Function Main: myString = "";
*******************************************************************************************/
// INTEGER
// LONG_INTEGER
// SIGNED_INTEGER
// SIGNED_LONG_INTEGER
// STRING

/*******************************************************************************************
  Functions
  (Add any additional functions here)
  Note:  Functions must be physically placed before the location in
         the code that calls them.
*******************************************************************************************/
/*
Function MyFunction1()
{
    // TODO:  Add local variable declarations here

    // TODO:  Add code here
}
*/

/*
Integer_Function MyIntFunction1()
{
    // TODO:  Add local variable declarations here

    // TODO:  Add code here

    Return (0);
}
*/

/*
String_Function MyStrFunction1()
{
    // TODO:  Add local variable declarations here

    // TODO:  Add code here

    Return ("");
}
*/

Function take_host(integer newhost)
{
   	makestring (TX$,"sw i0%d\x0D",newhost);
}

Function update_host_fb (integer curhost)
{
	integer loop;	
	for (loop = 1 to GetNumArrayCols(host_taken_fb))
		{
		if (loop = curhost)
			{
			host_taken_fb[loop] = 1;
			host_fb = loop;
			}
			else
				{
				host_taken_fb[loop] = 0;				
				}
		}
}

Function update_priority_fb (integer priority_host)
{
	priority_fb = priority_host;
	priority_none_fb = !priority_host;
}

Function update_auto_switch_enable_fb (integer auto_switch_enable_state)
{
	auto_switch_enable_fb = auto_switch_enable_state;
	auto_switch_disable_fb = !auto_switch_enable_state;

}
/*******************************************************************************************
  Event Handlers
  (Uncomment and declare additional event handlers as needed)
*******************************************************************************************/
/*
PUSH input
{
    // TODO:  Add code here
}
*/

PUSH poll_connected
{
	integer loop;
	for (loop = 1 to GetNumArrayCols(host_connected_fb))
		{
		makestring (TX$,"usb p0%d\x0D",loop);
		delay(10);
		}
}

PUSH poll_status
{
	makestring (TX$,"read\x0D");
}

PUSH host_take
{
    take_host(GetLastModifiedArrayIndex ());
}

PUSH priority_none
{
	makestring (TX$,"swmode priority off\x0D");
}

PUSH auto_switch_enable
{
	makestring (TX$,"pod on\x0D");
}

PUSH auto_switch_enable
{
	makestring (TX$,"pod on\x0D");
}


PUSH auto_switch_disable
{
	makestring (TX$,"pod off\x0D");
}

CHANGE host
{
    take_host(host);
}

CHANGE priority
{
   	makestring (TX$,"swmode priority i0%d\x0D",priority);

}

/*
RELEASE input
{
    // TODO:  Add code here
}
*/

/*
CHANGE input
{
    // TODO:  Add code here
}
*/
THREADSAFE CHANGE RX$
{
	String tmpString[65534];
	String Chomp$[32];
	integer which_port;
 
	While(1) // This While(1) is OK 
	{ 
		Try // Prevents an exception from exiting the event 
		{ 
		tmpString = Gather("\r", RX$); //Gather keeps the priority high until delimiter is found 
		// processing of tmpString
		trace ("parsing tmpString %s\n",tmpString);
		if (FIND("Command OK", tmpString))
			{
			trace ("Found Command OK in tmpString\n");
			if (FIND("sw i", tmpString))
				{
				Chomp$ = REMOVE ("C",tmpString);
            	update_host_fb (ATOI(Chomp$));
            	trace ("Updating feedback with host = %d",ATOI(Chomp$));
				}
				else if (FIND("pod on", tmpString))
					{	
					update_auto_switch_enable_fb(1);
					}
					else if (FIND("pod off", tmpString))
						{	
						update_auto_switch_enable_fb(0);
						}
						else if (FIND("swmode priority", tmpString))
							{
							if (FIND("off", tmpString))
								{
								update_priority_fb (0);	
								}
								else if (FIND("i0", tmpString))
									{
									update_priority_fb (ATOI(tmpString));	
									}
							}

			}

		if (FIND("Port:", tmpString) && FIND("connected)", tmpString))
			{
			trace ("Found connection info in tmpString\n");
			Chomp$ = REMOVE (")",tmpString);
			which_port=ATOI(Chomp$);
			trace ("Working with message %s, port = %d\n",Chomp$,which_port);
			if (FIND("Current", Chomp$)) // a reply to the "read" command, both current host and connection status
			  	{
            	update_host_fb (which_port);		  	
			  	}
			if (FIND("(not", Chomp$))
				{
				host_connected_fb[which_port] = 0;
				}
				else if (FIND("(connected", Chomp$))
					{
					host_connected_fb[which_port] = 1;							
					}
            }
			else if (FIND("POD: OFF", tmpString))
				{
				update_auto_switch_enable_fb(0);
				}
				else if (FIND("POD: ON", tmpString))
					{
					update_auto_switch_enable_fb(1);
					}
					else if (FIND("Priority Port:", tmpString))
						{
						update_priority_fb (ATOI(tmpString));	
						}

					            
		tmpString = "";
		} 
 		Catch
		{
		} 
	}      
}

/*
EVENT
{
    // TODO:  Add code here
}
*/

/*
SOCKETCONNECT
{
    // TODO:  Add code here
}
*/

/*
SOCKETDISCONNECT
{
    // TODO:  Add code here
}
*/

/*
SOCKETRECEIVE
{
    // TODO:  Add code here
}
*/

/*
SOCKETSTATUS
{
    // TODO:  Add code here
}
*/

/*******************************************************************************************
  Main()
  Uncomment and place one-time startup code here
  (This code will get called when the system starts up)
*******************************************************************************************/
/*
Function Main()
{
    // TODO:  Add code here
    // Initialize declared global and local variables/arrays as needed.

    // WaitForInitializationComplete();
    // If you are reading any Input or Output variables, uncomment
    //   the WaitForInitializationComplete statement above and read
    //   them afterwards.  Input/Output variables will not have
    //   their correct values set until after the logic processor
    //   runs and propagates the values to them.
}
*/

